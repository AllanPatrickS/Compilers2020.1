@@grammar::IMP
@@eol_comments :: /#.*?$/

@@keyword :: 'and'
@@keyword :: 'not'
@@keyword :: 'or'
@@keyword :: 'True'
@@keyword :: 'False'
@@keyword :: '+'
@@keyword :: '-'
@@keyword :: '(' ')'
@@keyword :: '=='
@@keyword :: '<'
@@keyword :: '<='
@@keyword :: '>'
@@keyword :: '>='
@@keyword :: ':='
@@keyword :: 'while' 
@@keyword :: 'if' 'then' 'else' 
@@keyword :: 'var' '='
@@keyword :: 'const' '='
@@keyword :: ';'
@@keyword :: 'skip'
@@keyword :: '{' '}'
@@keyword :: 'print'
@@keyword :: 'concat'
@@keyword :: 'append'

start = [ds:decSeq] [cs:cmd_seq] $ ;

exp = arr | arr_int | bin_exp | un_exp  | paren_exp | @:atom ;

arr = idn:identifier"["e:exp"]" | op:"concat" "(" e1:exp "," e2:exp  ")" | op:"append" "(" e1:exp "," e2:exp ")";

arr_int = "[" ','%{e:exp}* "]" ;

paren_exp = "(" e:exp ")" ;

bin_exp = l:arr_int op:binop e:exp | e1:exp op:binop e2:exp ;

un_exp = op:"len" "(" e:exp ")" | op:"not" e:exp ;

atom = @:num_atom  | @:truth_atom | @:id_atom ;

num_atom = number ;

truth_atom = truth ;

id_atom = identifier ;

binop = "and"| "or" | "==" | "<=" | ">=" | "<" | ">"  | "+" | "-" | "*" | "/"  ;

truth = 'True' | 'False' ;

# This makes sure that an identifier does not match a keyword.
@name
identifier = /(?!\d)\w+/ ;

number = /\d+/ ;

blk = "{" ds:decSeq cs:cmd_seq "}" ;

cmd_seq = ";"%{ac:atom_cmd}+ ;

atom_cmd = cond | loop | assign | print | call | skip ;

skip = "skip" ;

assign =  idn:identifier op:":=" e:exp | idn:identifier"["idx:exp"]" op:":=" e:exp ;

print = op:"print" e:exp ;

cond = op:"if" t:paren_exp b1:blk ["else" b2:blk] ;

loop = op:"while" t:paren_exp b:blk ;

dec = fn | rec | var | const ;

decSeq = { d:dec }* ;

var = op:"var" ","%{ idn:identifier "=" e:exp }+ ";" ;

const = op:"const" idn:identifier "=" e:exp ";" ;

fn = op:"def" idn:identifier '(' f:formal ')' b:blk ;

rec = op:"rec" idn:identifier '(' f:formal ')' b:blk ;

formal = ','%{ identifier }* ;

call = idn:identifier '(' a:actual ')' ;

actual = ','%{ exp }* ;



